apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "me.jp.sticker"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 18
        versionName "0.18"
    }


    signingConfigs {
        release {
            Properties props = new Properties()
            props.load(new FileInputStream(file(project.property("key_store.properties"))))
            storeFile file(props['RELEASE_STORE_JKS'])
            storePassword props['RELEASE_STORE_PASSWORD']
            keyAlias props['RELEASE_KEY_ALIAS']
            keyPassword props['RELEASE_KEY_PASSWORD']
        }
    }

    buildTypes {
        release {
            zipAlignEnabled true
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name == "app-release.apk") {
                        def fileName = "kittylive_test_${defaultConfig.versionCode}(${defaultConfig.versionName})_${releaseTime()}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }

                }
            }
        }
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:recyclerview-v7:23.4.0'
    compile 'com.android.support:design:23.4.0'
    // glide
    compile 'com.github.bumptech.glide:glide:3.7.0'
}

def releaseTime() {
    return new Date().format("MMdd_HHmm", TimeZone.getTimeZone("GMT+08:00"))
}